
//@skip-check module-structure-method-in-regions
//@skip-check module-accessibility-at-client
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	НужноРегистировать = Истина;
	
	Если Не ЭтоНовый() И Не ЕстьИзменения() Тогда
		НужноРегистировать = Ложь;  
	КонецЕсли;
	
	Если НужноРегистировать Тогда
		ЗарегистироватьКОбмену();
	КонецЕсли;
		
	
КонецПроцедуры


//@skip-check module-structure-method-in-regions
//@skip-check module-accessibility-at-client
Функция ЕстьИзменения()

	Если ЭтоНовый() Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ОбъектДоЗаписи = Ссылка.ПолучитьОбъект();
	
	Если ТипНомера <> ОбъектДоЗаписи.ТипНомера 
		ИЛИ ДатаЗаезда <> ОбъектДоЗаписи.ДатаЗаезда	
		ИЛИ ДатаВыезда <> ОбъектДоЗаписи.ДатаВыезда	
	    ИЛИ ЗавтракВключен <> ОбъектДоЗаписи.ЗавтракВключен
		ИЛИ НачалоДня(Дата) <> НачалоДня(ОбъектДоЗаписи.Дата) Тогда   
		
		Возврат Истина;
		
	КонецЕсли; 
	
	Если Постояльцы.Количество() <> ОбъектДоЗаписи.Постояльцы.Количество() Тогда
		Возврат Истина;	
	КонецЕсли; 
	
	Для Сч = 0 По Постояльцы.Количество() -1 Цикл
		Если Постояльцы[Сч].Постоялец <> ОбъектДоЗаписи.Постояльцы[Сч].Постоялец Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЦикла;	

	Возврат Ложь;
	
КонецФункции 

//@skip-check module-structure-method-in-regions
//@skip-check module-accessibility-at-client
Процедура ЗарегистироватьКОбмену()

	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ОбменМеждуСистемамиБронирования.Ссылка КАК Ссылка
	               |ИЗ
	               |	ПланОбмена.ОбменМеждуСистемамиБронирования КАК ОбменМеждуСистемамиБронирования
	               |ГДЕ
	               |	НЕ ОбменМеждуСистемамиБронирования.ЭтотУзел
	               |	И НЕ ОбменМеждуСистемамиБронирования.ПометкаУдаления";
	
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ОбменДанными.Получатели.Добавить(Выборка.Ссылка);
	КонецЦикла;
	
	
КонецПроцедуры
